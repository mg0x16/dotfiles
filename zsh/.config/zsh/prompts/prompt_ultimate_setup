source $ZSH_CONF_DIR/prompts/components/path.zsh
source $ZSH_CONF_DIR/prompts/components/git.zsh
source $ZSH_CONF_DIR/prompts/components/executeTime.zsh
source $ZSH_CONF_DIR/prompts/components/shellIndicator.zsh

function_exists() {
  declare -f -F $1 > /dev/null
  return $?
}

prompt_run_async_tasks() {
  async_init
  async_start_worker "prompt" -u -n
  async_register_callback "prompt" prompt_async_tasks_callback

  for element in "${PROMPT_ELEMENTS[@]}"; do
    function_exists "__prompt_component_${element}_async_run" &&
      async_job "prompt" "__prompt_component_${element}_async_run" $PWD
  done
}

prompt_async_tasks_callback() {
  local job=$1 code=$2 output=$3 exec_time=$4 next_pending=$6

  for element in "${PROMPT_ELEMENTS[@]}"; do
    if [[ $job == "__prompt_component_${element}_async_run" ]]; then
      function_exists "__prompt_component_${element}_async_callback" &&
        "__prompt_component_${element}_async_callback" $output

    fi
  done
  zle && zle .reset-prompt
}

prompt_pre_executing_command_hook() {
  local element
  for element in "${PROMPT_ELEMENTS[@]}"; do
    function_exists "__prompt_component_${element}_preexec" &&
      "__prompt_component_${element}_preexec"
  done
}

prompt_pre_printing_hook() {
  local element
  for element in "${PROMPT_ELEMENTS[@]}"; do
    function_exists "__prompt_component_${element}_precmd" &&
      "__prompt_component_${element}_precmd"
  done

  # run async
  if zplug check mafredri/zsh-async; then
    prompt_run_async_tasks
  fi
}

build_left_prompt() {
  local element
  for element in "${PROMPT_ELEMENTS[@]}"; do
    function_exists "__prompt_component_${element}_render" &&
      "__prompt_component_${element}_render"
  done
}

prompt_ultimate_setup() {
  # zsh hooks
  zmodload zsh/zle
  autoload -Uz add-zsh-hook

  # Intialize prompt options
  prompt_opts=(subst percent)

  # init function
  local element
  for element in "${PROMPT_ELEMENTS[@]}"; do
    function_exists "__prompt_component_${element}_init" &&
      "__prompt_component_${element}_init"
  done

  # hooks
  add-zsh-hook preexec prompt_pre_executing_command_hook
  add-zsh-hook precmd prompt_pre_printing_hook

  PROMPT='$(build_left_prompt)'
}

PROMPT_ELEMENTS=(path git executeTime shellIndicator)

prompt_ultimate_setup "$@"

# vim:ft=zsh
